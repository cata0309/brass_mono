#! /usr/local/bin/fontforge
# takes multiple svg's, positions and scales glyphs, outputs sfd and otf for each
i = 1
while ( i<$argc )
    file_name = $argv[i]
    if ( file_name:e != "svg" )
        Error(file_name + " is not an svg")
    endif

    # open the given svg file
    Open(file_name);

    # set encoding to add glyphs with no paths
    Reencode("iso8859-1");

    # add space and nbspace glyphs
    # and make them worth selecting
    Select(0x20, 0xA0);
    SetWidth(560);

    # select all relevant glyphs and make them
    # the right size in the right place
    # monospacing to 450
    SelectWorthOutputting();
    ScaleToEm(933, 267);
    Scale(120);
    Transform(100, 0, 0, 100, 4500, -27500);
    SetWidth(560);

    # Set font info
    SetFontNames("PeterCozy", "Peter Mono Cozy", "Peter Mono Cozy Regular", "Regular");

    # save sfd
    outfile=file_name:r + "_cozy.sfd";
    Save("forge/" + outfile);

    # generate open type font
    Generate("PeterMonoFonts/" + outfile:r + ".otf");

    # make non-cozy version
    SelectWorthOutputting();
    SetWidth(590);
    Transform(100, 0, 0, 100, 3000, 0);
    SetFontNames("Peter", "Peter Mono", "Peter Mono Regular", "Regular");

    outfile=file_name:r + ".sfd";
    Save("forge/" + outfile);
    Generate("PeterMonoFonts/" + outfile:r + ".otf");

    # make comfortable version
    SelectWorthOutputting();
    SetWidth(650);
    Transform(100, 0, 0, 100, 3000, 0);
    SetFontNames("PeterComfortable", "Peter Mono Comfortable", "Peter Mono Comfortable Regular", "Regular");

    outfile=file_name:r + "_comfortable.sfd";
    Save("forge/" + outfile);
    Generate("PeterMonoFonts/" + outfile:r + ".otf");

    i = i+1
endloop
